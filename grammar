# Khi grammar

## Document

<expression-document> = *
                      | *<expression>*

<dictionary-document> = *
                      | *<dictionary>*

<table-document> = *
                 | *<table>*

## Expression

<expression> = <expression'>
             | "::" <expression'>
<expression'> = <expression''>
              | <expression''> "::" <expression'>
<expression''> = <word>
               | <word> <expression''>
               | <transcription>
               | <transcription> <expression''>
               | <text-block>
               | <text-block> <expression''>
               | <bracketed-expression>
               | <bracketed-expression> <expression''>
               | <bracketed-dictionary>
               | <bracketed-dictionary> <expression''>
               | <bracketed-table>
               | <bracketed-table> <expression''>
               | <tuple>
               | <tuple> <expression''>
               | <tagged-value>
               | <tagged-value> <expression''>
               | "~"
               | "~" <expression''>

## Dictionary

<dictionary> = <flow-dictionary>
             | <bullet-dictionary>

<flow-dictionary> = <dictionary-entry>
                  | <dictionary-entry> ";"
                  | <dictionary-entry> ";" <flow-dictionary>

<bullet-dictionary> = ">" <dictionary-entry>
                    | ">" <dictionary-entry> <bullet-dictionary>

<dictionary-entry> = <word>":" <expression>
                   | <transcription>":" <expression>
                   | <text-block>":" <expression>

## Table

### All rows must have the same number of columns.
<table> = <flow-table>
        | <grid-table>
        | <bullet-table>

<flow-table> = <expression>
             | <expression> ";"
             | <expression> ";" <flow-table>
             | <expression> "|" <flow-table>

### Cannot have zero columns.
<grid-table> = "|" <expression> <grid-table>
             | "|" <grid-table>
             | "|"

<bullet-table> = ">" <expression>
               | ">" <expression> <bullet-table'>
<bullet-table'> = <bullet-table>
                | "|" <expression>
                | "|" <expression> <bullet-table'>

## Tuple

<tuple> = "<>"
        | "<>"<arguments>

## Tag & tagged value

### Name cannot start with a hash sign.
<tag> = "<"<word>">"
      | "<"<word> <attributes> ">"

<attributes> = <word>
             | <word> <attributes>
             | <word>":" <word>
             | <word>":" <word> <attributes>
             | <word>":" <transcription>
             | <word>":" <transcription> <attributes>
             | <word>":" <text-block>
             | <word>":" <text-block> <attributes>

<tagged-value> = <tag>
               | <tag><arguments>

## Arguments

<arguments> = ":"<word>
            | ":"<word><arguments>
            | ":"<transcription>
            | ":"<transcription><arguments>
            | ":"<text-block>
            | ":"<text-block><arguments>
            | ":"<bracketed-expression>
            | ":"<bracketed-expression><arguments>
            | ":"<bracketed-dictionary>
            | ":"<bracketed-dictionary><arguments>
            | ":"<bracketed-table>
            | ":"<bracketed-table><arguments>
            | ":""<>"
            | ":""<>"<arguments>
            | ":"<tag>
            | ":"<tag><arguments>
            | ":"_<tuple>
            | ":"_<tagged-value>

## Bracketed structures

<bracketed-expression> = "{" <expression> "}"

<bracketed-dictionary> = "{" "}"
                       | "{" <dictionary> "}"

<bracketed-table> = "[" "]"
                  | "[" <table> "]"

## Text & whitespace

### Matches a Word token.
<word>

### Matches a Transcription token.
<transcription>

### Matches a Text block token.
<text-block>

### Any whitespace
### Any number (including zero) of whitespace tokens.
### Also represented by a space character.
*

### Some whitespace
### At least one whitespace token.
_
