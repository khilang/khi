# Khi grammar

## Document

<expression-document> = *
                      | *<expression>*

<dictionary-document> = *
                      | *<dictionary>*

<table-document> = *
                 | *<table>*

## Expression

<expression> = <expression'>
             | <expression'> "<>" <expression'> # Constructor notation
<expression'> = <word>
              | <word> <expression'>
              | <quotation>
              | <quotation> <expression'>
              | <text-block>
              | <text-block> <expression'>
              | <bracketed-expression>
              | <bracketed-expression> <expression'>
              | <bracketed-dictionary>
              | <bracketed-dictionary> <expression'>
              | <bracketed-table>
              | <bracketed-table> <expression'>
              | <pattern>
              | <pattern> <expression'>
              | "~"
              | "~" <expression'>

## Dictionary

<dictionary> = <flow-dictionary>
             | <bullet-dictionary>

<flow-dictionary> = <dictionary-entry>
                  | <dictionary-entry> ";"
                  | <dictionary-entry> ";" <flow-dictionary>

<bullet-dictionary> = ">" <dictionary-entry>
                    | ">" <dictionary-entry> <bullet-dictionary>

<dictionary-entry> = <word>":" <expression>
                   | <quotation>":" <expression>
                   | <text-block>":" <expression>

## Table

### All rows must have the same number of columns.
<table> = <flow-table>
        | <grid-table>
        | <bullet-table>

<flow-table> = <expression>
             | <expression> ";"
             | <expression> ";" <flow-table>
             | <expression> "|" <flow-table>

### Cannot have zero columns.
<grid-table> = "|" <expression> <grid-table>
             | "|" <grid-table>
             | "|"

<bullet-table> = ">" <expression>
               | ">" <expression> <bullet-table'>
<bullet-table'> = <bullet-table>
                | "|" <expression>
                | "|" <expression> <bullet-table'>

## Pattern

### Name cannot start with a hash sign.
<tag> = "<"<word>">"
      | "<"<word> <attributes> ">"

<attributes> = <word>
             | <word> <attributes>
             | <word>":" <word>
             | <word>":" <word> <attributes>
             | <word>":" <quotation>
             | <word>":" <quotation> <attributes>
             | <word>":" <text-block>
             | <word>":" <text-block> <attributes>

<pattern> = <tag>
          | <tag><arguments>

<arguments> = ":"<word>
            | ":"<word><arguments>
            | ":"<quotation>
            | ":"<quotation><arguments>
            | ":"<text-block>
            | ":"<text-block><arguments>
            | ":"<bracketed-expression>
            | ":"<bracketed-expression><arguments>
            | ":"<bracketed-dictionary>
            | ":"<bracketed-dictionary><arguments>
            | ":"<bracketed-table>
            | ":"<bracketed-table><arguments>
            | ":"<tag>
            | ":"<tag><arguments>
            | ":""<>"":"<pattern>

## Bracketed structures

<bracketed-expression> = "{" <expression> "}"

<bracketed-dictionary> = "{" "}"
                       | "{" <dictionary> "}"

<bracketed-table> = "[" "]"
                  | "[" <table> "]"

## Text & whitespace

### Matches a Word token.
<word>

### Matches an Quotation token.
<quotation>

### Matches a Text block token.
<text-block>

### Any whitespace
### Any number (including zero) of whitespace tokens.
### Also represented by a space character.
*
